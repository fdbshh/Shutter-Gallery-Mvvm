def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

def androidId = System.getProperty("deviceId")

android {
  compileSdkVersion COMPILE_SDK_VERSION
  buildToolsVersion BUILD_TOOLS_VERSION

  signingConfigs {
    release {
      keyAlias 'shutter-app'
      keyPassword 'nupQcuuYsY#&N1p6'
      storeFile file('keystore/shutter-release.jks')
      storePassword 'nupQcuuYsY#&N1p6'
    }
  }

  defaultConfig {
    applicationId "com.tramsun.shutterstock"

    minSdkVersion MIN_SDK_VERSION
    targetSdkVersion TARGET_SDK_VERSION

    versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"

    testInstrumentationRunner "com.tramsun.shutterstock.MockApplicationTestRunner"

    vectorDrawables.useSupportLibrary = true

    buildConfigField 'String', 'SHUTTERSTOCK_BASE_URL', '"https://api.shutterstock.com/v2/"'
  }

  packagingOptions {
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/NOTICE.txt'
    exclude 'LICENSE.txt'
  }

  dataBinding {
    enabled = true
  }

  buildTypes {
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

/** ======= Unlock phone when running Instrumentation tests - BEGIN ======= */
if (androidId != null) {
  preBuild.dependsOn 'sendPowerCommand'
}

task sendPowerCommand(type: Exec) {
  def rootDir = project.rootDir
  def localProperties = new File(rootDir, "local.properties")
  Properties properties = new Properties()
  localProperties.withInputStream { instr -> properties.load(instr)
  }
  def sdkDir = properties.getProperty('sdk.dir')
  def adb = "$sdkDir/platform-tools/adb"
  commandLine "$adb", "-s", "$androidId", "shell", "input", "keyevent", "26"
}
/** ======= Unlock phone when running Instrumentation tests - END ======= */

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])

  // Android Support Libs
  compile "com.android.support:support-v13:${SUPPORT_LIB_VERSION}"
  compile "com.android.support:appcompat-v7:${SUPPORT_LIB_VERSION}"
  compile "com.android.support:design:${SUPPORT_LIB_VERSION}"

  //Dagger 2
  compile 'com.google.dagger:dagger:2.8'
  annotationProcessor 'com.google.dagger:dagger-compiler:2.8'
  annotationProcessor 'com.google.guava:guava:21.0'

  // Retrofit
  compile "com.squareup.retrofit2:retrofit:${RETROFIT_VERSION}"
  compile "com.squareup.retrofit2:converter-gson:${RETROFIT_VERSION}"
  compile "com.squareup.retrofit2:adapter-rxjava2:${RETROFIT_VERSION}"

  // OkHttp
  compile "com.squareup.okhttp3:okhttp:${OKHTTP_VERSION}"
  compile "com.squareup.okhttp3:logging-interceptor:${OKHTTP_VERSION}"

  // RxJava
  compile "io.reactivex.rxjava2:rxjava:${RXJAVA_VERSION}"
  compile "io.reactivex.rxjava2:rxandroid:${RXANDROID_VERSION}"

  // Marshmallow Permissions
  compile 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.3@aar'

  // Image Loading
  compile 'com.squareup.picasso:picasso:2.5.2'

  // Rich ImageView
  compile 'com.davemorrissey.labs:subsampling-scale-image-view:3.6.0'

  // Infinite Scrolling
  compile 'com.github.pwittchen:infinitescroll:0.0.1'

  // Logging
  compile "com.jakewharton.timber:timber:${TIMBER_VERSION}"

  testAnnotationProcessor 'com.google.dagger:dagger-compiler:2.8'
  testCompile 'junit:junit:4.12'
  testCompile 'org.mockito:mockito-core:2.6.3'
  testCompile "com.google.truth:truth:0.31"

  androidTestAnnotationProcessor 'com.google.dagger:dagger-compiler:2.8'
  androidTestCompile 'org.mockito:mockito-core:2.6.3'
  androidTestCompile("com.android.support.test.espresso:espresso-intents:${ESPRESSO}") {
    exclude group: 'com.android.support', module: 'design'
    exclude group: 'com.android.support', module: 'appcompat-v7'
    exclude group: 'com.android.support', module: 'appcompat'
    exclude group: 'com.android.support', module: 'support-v4'
    exclude group: 'com.android.support', module: 'support-annotations'
    exclude module: 'recyclerview-v7'
  }

  androidTestCompile("com.android.support.test.espresso:espresso-contrib:${ESPRESSO}") {
    exclude group: 'com.android.support', module: 'design'
    exclude group: 'com.android.support', module: 'appcompat-v7'
    exclude group: 'com.android.support', module: 'appcompat'
    exclude group: 'com.android.support', module: 'support-v4'
    exclude group: 'com.android.support', module: 'support-annotations'
    exclude module: 'recyclerview-v7'
  }
  androidTestCompile("com.android.support.test.espresso:espresso-core:${ESPRESSO}") {
    exclude group: 'com.android.support', module: 'design'
    exclude group: 'com.android.support', module: 'appcompat-v7'
    exclude group: 'com.android.support', module: 'appcompat'
    exclude group: 'com.android.support', module: 'support-v4'
    exclude group: 'com.android.support', module: 'support-annotations'
    exclude module: 'recyclerview-v7'
  }
}
